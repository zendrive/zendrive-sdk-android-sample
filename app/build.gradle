apply plugin: 'com.android.application'

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.zendrive.zendrivesdkdemo"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        resValue "bool", "isTestingMode", "false"
        buildConfigField "Class<? extends LayoutHandler>",
                "layoutHandler", "DefaultLayoutHandler.class"
    }

    buildTypes {
        zendrivesdktesting {
            initWith debug
            resValue "bool", "isTestingMode", "true"
            buildConfigField "Class<? extends LayoutHandler>",
                    "layoutHandler",
                    "MockDriveLayoutHandler.class"
            applicationIdSuffix '.zendrivesdktesting'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        // This is needed to avoid spurious lint errors from libthrift on android.
        disable 'InvalidPackage'
        abortOnError true
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    buildFeatures {
        dataBinding = true
        aidl = false
        renderScript = false
        shaders = false
    }
    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

dependencies {
    def zendrive_sdk_version = '10.0.2'
    implementation "com.zendrive.sdk.android:ZendriveSDK:$zendrive_sdk_version"
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    zendrivesdktestingImplementation "com.zendrive.sdk.android:ZendriveSDK-testing:$zendrive_sdk_version"
}
